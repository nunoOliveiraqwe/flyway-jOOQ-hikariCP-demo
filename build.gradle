plugins {
    id 'java'
    id 'org.flywaydb.flyway' version '9.7.0'
    id 'nu.studer.jooq' version '7.1'
}

group 'pt.shorty'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}


configurations {
    flywayMigration
}

dependencies {

    flywayMigration 'org.xerial:sqlite-jdbc:3.40.0.0'
    jooqGenerator 'org.xerial:sqlite-jdbc:3.40.0.0'

    implementation 'org.jooq:jooq:3.17.7'
    implementation  "org.flywaydb:flyway-core:8.2.0"
    implementation 'org.xerial:sqlite-jdbc:3.40.0.0'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

}

def migrationDB = projectDir.getAbsolutePath()+'/migration.db'

flyway {
    configurations = ['flywayMigration']
    url = 'jdbc:sqlite:'+migrationDB
    locations = ['filesystem:src/main/resources/db/migration']
    user = ''
    password = ''
}

jooq {
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    url = flyway.url
                    user = flyway.user
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.sqlite.SQLiteDatabase'
                        includes = '.*'
                        excludes = ''
                    }
                   generate {
                       pojos = true
                       daos = true
                   }
                    target {
                        packageName = 'db.model'
                    }
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}

// configure jOOQ task such that it only executes when something has changed that potentially affects the generated JOOQ sources
// - the jOOQ configuration has changed (Jdbc, Generator, Strategy, etc.)
// - the classpath used to execute the jOOQ generation tool has changed (jOOQ library, database driver, strategy classes, etc.)
// - the schema files from which the schema is generated and which is used by jOOQ to generate the sources have changed (scripts added, modified, etc.)
tasks.named('generateJooq').configure {
    // ensure database schema has been prepared by Flyway before generating the jOOQ sources
    dependsOn tasks.named('flywayMigrate')

    // declare Flyway migration scripts as inputs on the jOOQ task
    inputs.files(fileTree('src/main/resources/db/migration'))
            .withPropertyName('migrations')
            .withPathSensitivity(PathSensitivity.RELATIVE)

    // make jOOQ task participate in incremental builds (and build caching)
    allInputsDeclared = true
}


task cleanUpMigrationDb(type: Delete) {
    delete files(migrationDB)
}

generateJooq.finalizedBy cleanUpMigrationDb